/*
 * Creates the **[ingen.settings.plugins.03-tickets-module]** plugin Active Event for
 * the settings module.
 */
create-event:ingen.settings.plugins.03-tickets-module

  /*
   * Returning widget necessary to include settings for "has ticket module" widget
   * in the settings module.
   */
  return
    label
      widgets
        input
          type:checkbox
          .data-field:ingen.settings.show-price-information
          onchange

            /*
             * Checking if checkbox is check on, at which point we'll have to display
             * SMTP/email settings - Otherwise we hide SMTP/email settings.
             */
            get-widget-property:x:/../*/_event?value
              checked
            if:x:/-/*/*

              /*
               * Price information is turned on.
               * Displaying SMTP/email settings, since there are no reasons to allow user
               * to configure them.
               */
              set-widget-property:smtp-settings
                style:""

              /*
               * Giving user some general information about what he needs to do to enable
               * the ticket module.
               */
              create-widgets
                micro.widgets.modal:ticket-settings-info
                  widgets
                    h3
                      innerValue:Information
                    p
                      innerValue:@"If you wish to have the system automatically send an email to your guests,
with the ticket information, as they purchase tickets - You'll have to configure your SMTP and email
settings. If you don't change these settings, no such emails will be sent."
                    div
                      class:right
                      widgets
                        button
                          innerValue:OK
                          oninit
                            micro.page.set-focus:x:/../*/_event?value
                          onclick
                            delete-widget:ticket-settings-info

            else

              /*
               * Price information is turned off.
               * Hiding SMTP/email settings, since there are no reasons to allow user
               * to configure them.
               */
              set-widget-property:smtp-settings
                style:"display:none;"

          oninit

            /*
             * Checking if tickets module is turned on.
             */
            if
              ingen.settings.show-price-information
              =:bool:true

              /*
               * Price information is turned on.
               */
              set-widget-property:x:/../*/_event?value
                checked

            else

              /*
               * Hiding SMTP/email settings, since there are no reasons to allow user to
               * configure them.
               */
              set-widget-property:smtp-settings
                style:"display:none;"

        span
          innerValue:Ticket module
