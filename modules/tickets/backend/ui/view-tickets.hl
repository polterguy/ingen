/*
 * Displays ticket information in a modal window and allows operator
 * to mark tickets as used.
 */


/*
 * Sanity checking invocation.
 */
micro.lambda.contract.min:x:/..
  id:long


/*
 * Verifying access.
 */
ingen.access._throw-if-no-access:false
  path:/tickets/backend/


/*
 * Retrieving ticket information.
 */
eval-x:x:/+/*
micro.evaluate.file:@INGEN/modules/tickets/backend/model/get-ticket.hl
  id:x:/../*/id?value


/*
 * Creates a modal window displaying ticket information.
 */
eval-x:x:/+/**/.id
create-widgets
  micro.widgets.modal:view-tickets
    class:micro-widgets-modal larger-text
    widgets

      /*
       * Descriptive header for modal window.
       */
      h2
        innerValue:x:/@micro.evaluate.file/*/exhibit?value

      /*
       * Information about ticket (name, email, etc).
       */
      p
        innerValue:"Name: {0}"
          :x:/@micro.evaluate.file/*/name?value
      p
        innerValue:"Email: {0}"
          :x:/@micro.evaluate.file/*/email?value
      p
        innerValue:"Adults: {0}"
          :x:/@micro.evaluate.file/*/adults?value
      p
        innerValue:"Children: {0}"
          :x:/@micro.evaluate.file/*/children?value

      /*
       * Button strip to allow operator to mark tickets as used, and close modal widget.
       */
      div
        class:right strip
        widgets

          /*
           * "Mark tickets as used" button.
           */
          button
            innerValue:Mark as used
            oninit

              /*
               * Setting initial focus to "mark as used" button.
               */
              micro.page.set-focus:x:/../*/_event?value

            onclick

              /*
               * Forward evaluated above.
               */
              .id:x:/../*/id?value

              /*
               * Making sure we set all tickets in collection to "used".
               */
              eval-x:x:/+/*
              micro.evaluate.file:@INGEN/modules/tickets/backend/model/set-all-tickets-used.hl
                id:x:/../*/.id?value

              /*
               * If we came this far, all tickets were used, and we can safely re-databind
               * the tickets datagrid.
               */
              ingen.tickets.re-databind

              /*
               * Deleting modal widget.
               */
              delete-widget:view-tickets

          /*
           * "Close modal widget" button.
           */
          button
            innerValue:Close
            onclick

              /*
               * Simply deleting modal widget.
               */
              delete-widget:view-tickets
