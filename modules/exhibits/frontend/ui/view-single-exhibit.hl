/*
 * File responsible for viewing a single exhibit.
 */


/*
 * Sanity checking invocation.
 */
micro.lambda.contract.min:x:/..
  id:long


/*
 * Verifying access.
 */
ingen.access._throw-if-no-access:true
  path:/exhibits/frontend/


/*
 * Making sure we remove eny previously created exhibits.
 */
clear-widget:exhibits-display-single-exhibit


/*
 * Retrieving exhibit from data layer.
 */
eval-x:x:/+/*
micro.evaluate.file:@INGEN/modules/exhibits/common/model/get-exhibit.hl
  id:x:/../*/id?value


/*
 * Transforming description markdownto HTML.
 */
micro.markdown.markdown2html:x:/@micro.evaluate.file/*/description?value


/*
 * Retrieving all "exhibits plugins", which in the core implies only the ticket module's
 * purchase tickets widget, but can easily be extended by creating an Active Event, who's
 * name is [ingen.exhibits.plugins.xxx], where "xxx" is some unique name, describing your
 * exhibits plugin.
 */
vocabulary:~ingen.exhibits.plugins.frontend.
for-each:x:/-/*?value

  /*
   * Invoking plugin event, and adding its result to our widget collection below.
   */
  set:x:/+2?name
    src:x:/@_dp?value
  eval-x:x:/+/*
  foo
    id:x:/../*/id?value
  add:x:/../*/create-widgets/*/div/=view-exhibit-wrapper/*/widgets
    src:x:/./-/*


/*
 * Displaying exhibit information in a modal widget, and allowing user to purchase ticket(s) for exhibit.
 */
eval-x:x:/+/**/.id
create-widgets
  div:view-exhibit-wrapper
    parent:exhibits-display-single-exhibit
    widgets
      h1:exhibit-header
        widgets
          span
            innerValue:x:/@micro.evaluate.file/*/name?value
          small
            innerValue:@" - Aults €{0}, Children €{1}"
              :x:/@micro.evaluate.file/*/price?value
              :x:/@micro.evaluate.file/*/children_price?value
      div
        class:themepark-view-single-exhibit
        innerValue:x:/@micro.markdown.markdown2html?value


/*
 * Making sure we scroll exhibit into view.
 */
micro.page.scroll-into-view:exhibit-header