/*
 * File responsible for creating a new or editing an existing database
 * entry of type _"exhibit"_. Expects **[name]**, **[description]**, **[price]**,
 * **[children_price]** and optionally **[id]**. If the latter is supplied, it
 * will update the existing record matching the **[id]**, instead of inserting a
 * new record.
 */


/*
 * Sanity checking arguments.
 */
micro.lambda.contract.min:x:/..
  name:string
  description:string
  price:int
  children_price:int
micro.lambda.contract.optional:x:/..
  id:long


/*
 * Connecting to ingen database and inserting new "exhibit" item.
 */
p5.mysql.connect:[ingen]

  /*
   * Checking if this is an update or an insert operation.
   */
  if:x:/../*/id?value
    and:x:/../*/id?value
      !=:

    /*
     * Update operation.
     */
    p5.mysql.update:update exhibits set name = @name, price = @price, children_price = @children_price, description = @description where id = @id
      @name:x:/../*/name?value
      @description:x:/../*/description?value
      @price:x:/../*/price?value.int
      @children_price:x:/../*/children_price?value.int
      @id:x:/../*/id?value.long

  else

    /*
     * Insert operation.
     */
    p5.mysql.insert:insert into exhibits (name, price, children_price, description) value (@name, @price, @children_price, @description)
      @name:x:/../*/name?value
      @description:x:/../*/description?value
      @price:x:/../*/price?value.int
      @children_price:x:/../*/children_price?value.int
