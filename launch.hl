/*
 * Main launch file for InGen.
 */


/*
 * Clearing widgets from main "cnt" widget, in case something else has already been
 * loaded here.
 */
clear-widget:cnt


/*
 * Including Magic Forrest skin and Micro's CSS files.
 */
micro.css.include
  skin:magic-forrest


/*
 * Including main CSS file for app.
 */
p5.web.include-css-file:@INGEN/media/main.css


/*
 * Ensuring we include PayPal's JavaScript file on initial request.
 */
micro.widgets.paypal-button.ensure-checkout-js


/*
 * Verifying that we're logged in, and if we are, we dynamically add the backend buttons
 * to our main wire frame form.
 */
whoami
if:x:/@whoami/*/role?value
  !=:guest

  /*
   * Loading up front end.
   */
  add:x:/../*/create-widget/**/widgets/=main-content-widgets
    load-file:@INGEN/back-end/admin-jumbo-button.hl


/*
 * Creating main front end wire frame.
 */
create-widget
  class:container
  oninit

    /*
     * starting pingback loop.
     */
    themepark.pingback.keep-alive

  events

    /*
     * Pingback event to keep session alive on server, in case app is in kiosk mode.
     */
    themepark.pingback.keep-alive

      /*
       * In case app is in "kiosk mode", we create a pingback to server ever 10 minutes, to avoid
       * destroying session, and keeping the client "alive".
       */
      micro.lambda.create-timeout
        milliseconds:600000
        onfinish

          /*
           * Recursively invoking event creating new timer.
           */
          themepark.pingback.keep-alive

  widgets
    div
      class:row
      widgets
        div:themepark-main-toolbar
          class:col
          widgets

            /*
             * The general toolbar for our app.
             */
            micro.widgets.file
              class:strip themepark-top-buttons
              folder:@INGEN/common/toolbar-buttons/

            /*
             * Header for app.
             */
            literal:themepath-header
              element:h1
              class:themepark-header center
              events

                /*
                 * Used by different modules when the header should be changed for some reasons.
                 */
                themepark.header.set

                  /*
                   * Checking if caller specified a value, and if not, resetting header to
                   * original value.
                   */
                  if:x:/../*/_arg?value
                    not

                    /*
                     * Resetting header and title to default value.
                     */
                    set-widget-property:x:/../*/_event?value
                      innerValue:InGen Theme Park
                    p5.web.page.set-title:InGen Theme Park

                  else

                    /*
                     * Setting header and title to specified [_arg] value.
                     */
                    set-widget-property:x:/../*/_event?value
                      innerValue:x:/../*/_arg?value
                    p5.web.page.set-title:x:/../*/_arg?value

            /*
             * Main content surface for our app.
             */
            div:themepark-main-content
              class:center themepark-center-content
              widgets:main-content-widgets
                button
                  class:jumbo-button
                  innerValue:@"<span class=""icon-happy""></span> Exhibits"
                  onclick

                    /*
                     * Dynamically loading exhibits.
                     */
                    micro.evaluate.file:@INGEN/front-end/exhibits/view-exhibits.hl

                    /*
                     * Evaluating file responsible for toggling Jumbo Buttons.
                     */
                    eval-x:x:/+/*
                    micro.evaluate.file:@INGEN/common/toggle-jumbo-button.hl
                      button:x:/../*/_event?value

            /*
             * Helper container widget, for displaying "view content".
             */
            div:themepark-dynamic-content
              class:center themepark-center-additional-content
              widgets


/*
 * Re-setting initial title.
 */
themepark.header.set
