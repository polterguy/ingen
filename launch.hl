/*
 * Main launch file for InGen.
 */


/*
 * Clearing widgets from main "cnt" widget, in case something else has already been
 * loaded here.
 */
clear-widget:cnt


/*
 * Including Magic Forrest skin and Micro's CSS files.
 */
micro.css.include
  skin:magic-forrest


/*
 * Including main CSS file for app.
 */
p5.web.include-css-file:@INGEN/media/main.css


/*
 * Ensuring we include PayPal's JavaScript file on initial request,
 * since we might need to accept payments in app.
 */
micro.widgets.paypal-button.ensure-checkout-js


/*
 * The next two pieces of code might need some explanation, since this is the "access control"
 * of the system.
 *
 * It invokes the "get-jumbo-buttons.hl" file inside of the "/imp/" folder, and adds the result
 * of the invocation to that file into the Jumbo Buttons widget below on page. This file internally
 * invokes [p5.auth.has-access], passing in the [default-access] argument as the default argument.
 * This results in that by default all users, insluding guest visitors, have access to all "frontend"
 * modules - While only a root account has access to the "backend" modules.
 *
 * This can be overridden by creating access objects in your installation using e.g. "Peeples" to
 * associate such access objects with the access object list of the system.
 */


/*
 * Adding all Jumbo Buttons for frontend modules according to whether or not
 * user has access to the modules or not.
 *
 * Notice, we default access to "true".
 */
add:x:/../*/create-widget/**/micro.widgets.file/=themepark-jumbo-buttons
  micro.evaluate.file:@INGEN/imp/get-jumbo-buttons.hl
    folder:@INGEN/imp/frontend/
    default-access:true


/*
 * Adding all Jumbo Buttons for backend modules according to whether or not
 * user has access to the modules or not.
 *
 * Notice, we default access to "false".
 */
add:x:/../*/create-widget/**/micro.widgets.file/=themepark-jumbo-buttons
  micro.evaluate.file:@INGEN/imp/get-jumbo-buttons.hl
    folder:@INGEN/imp/backend/
    default-access:false


/*
 * Creating main front end wire frame.
 */
create-widget
  class:container
  oninit

    /*
     * Starting pingback loop.
     */
    themepark.pingback.keep-alive

  events

    /*
     * Pingback event to keep session alive on server, in case app is in kiosk mode.
     */
    themepark.pingback.keep-alive

      /*
       * In case app is in "kiosk mode", we create a pingback to server ever 10 minutes,
       * to avoid destroying session, and keeping the client "alive".
       */
      micro.lambda.create-timeout
        milliseconds:600000
        onfinish

          /*
           * Recursively invoking event creating new timer.
           */
          themepark.pingback.keep-alive

  widgets
    div
      class:row
      widgets
        div:themepark-main-toolbar
          class:col
          widgets

            /*
             * The general toolbar for our app.
             * This is the "toolbar" displayed at the top/right corner of InGen app.
             */
            micro.widgets.file
              class:strip themepark-top-buttons
              folder:@INGEN/imp/common/toolbar-buttons/

            /*
             * Header for app.
             */
            literal:themepath-header
              element:h1
              class:themepark-header center
              events

                /*
                 * Used by different modules when the header should be changed for some reasons.
                 */
                themepark.header.set

                  /*
                   * Checking if caller specified a value, and if not, resetting header to
                   * original value.
                   */
                  if:x:/../*/_arg?value
                    not

                    /*
                     * Resetting header and title to default value.
                     */
                    set-widget-property:x:/../*/_event?value
                      innerValue:InGen Theme Park
                    p5.web.page.set-title:InGen Theme Park

                  else

                    /*
                     * Setting header and title to specified [_arg] value.
                     */
                    set-widget-property:x:/../*/_event?value
                      innerValue:x:/../*/_arg?value
                    p5.web.page.set-title:x:/../*/_arg?value

            /*
             * Main content surface for Jumbo Buttons in our app.
             *
             * These are our "launch modules" buttons, that allows the user to interact with
             * the app in general, and launches modules, such as "front end exhibit module", etc.
             *
             * This widget is dynamically populated according to the access control objects in P5, using
             * [p5.auth.has-access]. This occurs in the "get-jumbo-buttons.hl" file which is invoked twice
             * further up on page, passing in frontend module (default to access granted) and backend
             * modules (default to access denied).
             */
            micro.widgets.file:themepark-jumbo-buttons
              class:center themepark-center-content

            /*
             * Container widget, for displaying dynamic "view content" for modules.
             */
            container:themepark-dynamic-content
              class:center themepark-center-additional-content


/*
 * Re-setting initial title.
 */
themepark.header.set


/*
 * Checking if this is a request for a tour.
 */
micro.url.get-entities
if:x:/@micro.url.get-entities/1?name
  =:tour

  /*
   * Automatically launching tour.
   */
  eval-x:x:/+/*
  micro.evaluate.file:@INGEN/imp/frontend/tours/ui/view-single-tour.hl
    id:x:/@micro.url.get-entities/2?name.long