/*
 * Creates our "view exhibits" front end widget.
 */


/*
 * Checking if "view exhibit" widget already exists, at which point we simply
 * return early.
 */
if
  fetch:x:/0/0?value
    widget-exists:view-exhibits-wrapper

  /*
   * Widget is already visible.
   */
  return


/*
 * Clearing previously create widgets from dynamic content wrapper widget.
 */
clear-widget:themepark-dynamic-content


/*
 * Setting header and title of page.
 */
themepark.header.set:Browse exhibits


/*
 * Creating our "view exhibit" datagrid in a "bg" wrapper div.
 */
create-widget:view-exhibits-wrapper
  parent:themepark-dynamic-content
  class:shaded rounded air air-inner bg
  widgets

    /*
     * Our actual datagrid, dynamically databound towards "exhibits" table.
     */
    micro.widgets.mysql.datagrid:view-exhibits-datagrid
      class:hover striped themepark-datagrid
      database:ingen
      table:exhibits
      .row
        onclick

          /*
           * Evaluating file responsible for displaying information for exhibit,
           * and allowing user to purchase ticket(s) to exhibit.
           */
          eval-x:x:/+/*
          micro.evaluate.file:@INGEN/front-end/exhibits/ux/view-single-exhibit.hl
            id:x:/../*/.row/*/id?value

      columns
        id
          visible:bool:false
        name
        price
          style:"width:5px;"
        children_price
          visible:bool:false
        children
          style:"width:5px;"
          .lambda
            eval-x:x:/+/*/*
            return
              span
                innerValue:x:/../*/row/*/children_price?value

    /*
     * A simple close button to simply delete view.
     */
    div
      class:right
      widgets
        button
          class:large
          innerValue:@"<span class=""icon-cross""></span>"
          onclick

            /*
             * Simply deletes view.
             */
            delete-widget:view-exhibits-wrapper

            /*
             * Clearing toggled state of any toggled Jumbo Buttons.
             */
            micro.evaluate.file:@INGEN/common/toggle-jumbo-button.hl
