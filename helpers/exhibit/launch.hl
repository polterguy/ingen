/*
 * Creating modal widget to show user exhibits.
 */
create-widgets
  micro.widgets.modal:exhibits-modal
    widgets
      h3
        innerValue:Exhibits
      div
        widgets
          micro.widgets.grid:exhibits-grid
            class:hover striped
            columns
              name
              when
            events
              ingen.exhibits-grid.databind

                /*
                 * Populating datagrid.
                 */
                .data
                if:x:/../name?value
                  p5.mysql.connect:[ingen]
                    p5.mysql.select:select * from exhibit where name like @name limit 10
                    add:x:/@.data
                      src:x:/@p5.mysql.select/*
                else
                  p5.mysql.connect:[ingen]
                    p5.mysql.select:select * from exhibit limit 10
                    add:x:/@.data
                      src:x:/@p5.mysql.select/*
                for-each:x:/@.data/*
                  eval-x:x:/+/*/*/*(/name|/when)|/+/*/*/*/*/*/.id
                  add:x:/../*/micro.widgets.grid.databind
                    src
                      item
                        .row
                          onclick
                            .id:x:/@_dp/#/*/id?value.long
                            p5.mysql.connect:[ingen]
                              p5.mysql.select:select * from exhibit where id = @id
                                @id:x:/@.id?value
                              markdown2html:x:/@p5.mysql.select/*/*/description?value
                              eval-x:x:/+/**/.id
                              create-widgets
                                micro.widgets.modal
                                  widgets
                                    h3
                                      innerValue:x:/@p5.mysql.select/*/*/name?value
                                    pre
                                      innerValue:x:/@markdown2html?value
                                    micro.widgets.paypal-button
                                      price:400
                                      .onok
                                        .id:x:/@p5.mysql.select/*/*/id?value.long
                                        create-widgets
                                          micro.widgets.modal
                                            widgets
                                              h3
                                                innerValue:Ticket
                                              p
                                                innerValue:Please take a photo of this screen with your phone.
                                              literal
                                                element:h4
                                                oninit
                                                  .id:x:/@.id?value.long
                                                  p5.types.guid.new
                                                  set-widget-property:x:/../*/_event?value
                                                    innerValue:x:/@p5.types.guid.new?value
                                                  p5.mysql.connect:[ingen]
                                                    p5.mysql.insert:insert into ticket (ticket_unique_id, exhibit_id, used) values (@ticket_unique_id, @exhibit_id, @used)
                                                      @ticket_unique_id:x:/@p5.types.guid.new?value.string
                                                      @exhibit_id:x:/@.id?value
                                                      @used:int:0
                        name:x:/@_dp/#/*/name?value
                        when:x:/@_dp/#/*/when?value
                micro.widgets.grid.databind:exhibits-grid

            oninit

              /*
               * Initially databinding grid.
               */
              ingen.exhibits-grid.databind

      div
        class:right
        widgets
          button
            class:Close
            innerValue:Close
            onclick
              delete-widget:exhibits-modal
