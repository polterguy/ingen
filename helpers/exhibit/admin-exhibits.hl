
create-widgets
  micro.widgets.modal:admin-exhibits
    class:micro-widgets-modal large
    widgets
      h3
        innerValue:Administrate exhibits
      micro.widgets.mysql.datagrid:admin-exhibits-grid
        class:hover striped
        database:ingen
        table:exhibit
        columns
          id
            visible:false
          name
          when
          price
          description
            visible:bool:false
          description
            .lambda

              /*
               * Transforming description to markdown and creating a pre widget.
               */
              p5.markdown.markdown2html:x:/../*/row/*/description?value
              eval-x:x:/+/*/*/*/.name
              eval-x:x:/+/*/*
              return
                pre
                  innerValue:x:/@p5.markdown.markdown2html?value
          delete
            .lambda
              eval-x:x:/+/*/*/*/.id
              return
                button
                  innerValue:@"<span class=""icon-bin""></span>"
                  onclick
                    .id:x:/../*/row/*/id?value.long
                    p5.mysql.connect:[ingen]
                      p5.mysql.delete:delete from exhibit where id = @id
                        @id:x:/@.id?value
                    micro.windows.info:Item deleted successfully ...
                      class:micro-windows-info success
                    micro.widgets.mysql.datagrid.databind:admin-exhibits-grid

      /*
       * Add button.
       */
      div
        class:right
        widgets
          button
            innerValue:+
            onclick
              create-widgets
                micro.widgets.modal:create-new-exhibit
                  widgets
                    div
                      class:strip fill
                      widgets
                        label
                          innerValue:Name
                          class:description-9
                        input
                          type:text
                          .data-field:name
                          placeholder:Name ...
                    div
                      class:strip fill
                      widgets
                        label
                          innerValue:Date
                          class:description-9
                        input
                          type:date
                          .data-field:date
                          placeholder:Date ...
                    div
                      class:strip fill
                      widgets
                        label
                          innerValue:Price
                          class:description-9
                        input
                          type:number
                          .data-field:price
                          placeholder:Price ...
                    label
                      innerValue:Name
                    micro.widgets.codemirror
                      mode:markdown
                      .data-field:description
                    div
                      class:right
                      widgets
                        button
                          innerValue:Save
                          onclick
                            micro.form.serialize:create-new-exhibit
                            p5.mysql.connect:[ingen]
                              p5.mysql.insert:insert into exhibit (name, `when`, description, price) value (@name, @when, @description, @price)
                                @name:x:/@micro.form.serialize/*/name?value
                                @when:x:/@micro.form.serialize/*/date?value
                                @description:x:/@micro.form.serialize/*/description?value
                                @price:x:/@micro.form.serialize/*/price?value.int
                            delete-widget:create-new-exhibit
                            micro.widgets.mysql.datagrid.databind:admin-exhibits-grid
                            micro.windows.info:Successfully inserted into exhibits ...
                              class:micro-windows-info success

