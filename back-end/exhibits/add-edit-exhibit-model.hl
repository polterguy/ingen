/*
 * File responsible for creating a new database entry of type "exhibit".
 */


/*
 * Signal node, used to separate arguments from the rest of our lambda.
 */
.signal


/*
 * Sanity checking arguments.
 */
micro.lambda.contract.min:x:/..
  name:string
  description:string
  price:int
micro.lambda.contract.optional:x:/..
  id:long


/*
 * Connecting to ingen database and inserting new "exhibit" item.
 */
p5.mysql.connect:[ingen]

  /*
   * Checking if this is an update or an insert operation.
   */
  if:x:/../*/id?value
    and:x:/../*/id?value
      !=:

    /*
     * Update operation.
     */
    p5.mysql.update:update exhibits set name = @name, price = @price, description = @description where id = @id
      @name:x:/@.signal/--/name?value
      @description:x:/@.signal/--/description?value
      @price:x:/@.signal/--/price?value.int
      @id:x:/@.signal/--/id?value.long

  else

    /*
     * Insert operation.
     */
    p5.mysql.insert:insert into exhibits (name, price, description) value (@name, @price, @description)
      @name:x:/@.signal/--/name?value
      @description:x:/@.signal/--/description?value
      @price:x:/@.signal/--/price?value.int
