/*
 * Creates our "admin exhibits" back end widget.
 */


/*
 * Checking if "manage exhibit" widget already exists, at which point we simply
 * return early.
 */
if
  fetch:x:/0/0?value
    widget-exists:manage-exhibits-wrapper

  /*
   * Widget is already visible.
   */
  return


/*
 * Clearing previously create widgets from dynamic content wrapper widget.
 */
clear-widget:themepark-dynamic-content


/*
 * Setting header and title of page.
 */
themepark.header.set:Administrate exhibits


/*
 * Creating our "manage exhibit" datagrid in a "bg" wrapper div.
 */
create-widget:manage-exhibits-wrapper
  parent:themepark-dynamic-content
  class:shaded rounded air air-inner bg
  widgets

    /*
     * Our actual datagrid, dynamically databound towards "exhibits" table.
     */
    micro.widgets.mysql.datagrid:manage-exhibits-datagrid
      class:hover striped themepark-datagrid
      database:ingen
      table:exhibits
      .row
        onclick

          /*
           * Invoking file responsible for editing a specific item from database.
           */
          micro.windows.info:Foo
          eval-x:x:/+/*
          micro.evaluate.file:@INGEN/back-end/exhibits/add-edit-exhibit-view.hl
            id:x:/../*/.row/*/id?value

      columns
        id
          visible:bool:false
        name
        price
          style:"width:5px;"
        delete
          style:"width:5px;"
          .lambda
            eval-x:x:/+/*/*/onclick/*/.id
            return
              button
                innerValue:@"<span class=""icon-bin""></span>"
                onclick

                  /*
                   * Forward evaluated above.
                   */
                  .id:x:/../*/row/*/id?value

                  /*
                   * Invoking file responsible for deleting exhibit.
                   */
                  eval-x:x:/+/*
                  micro.evaluate.file:@INGEN/back-end/exhibits/delete-exhibit-model.hl
                    id:x:/@.id?value

                  /*
                   * Re-databinding datagrid.
                   */
                  micro.widgets.mysql.datagrid.databind:manage-exhibits-datagrid

                  /*
                   * Provding some visual feedback to user.
                   */
                  micro.windows.info:Successfully updated exhibits
                    class:micro-windows-info success

    /*
     * Toolbar at the bottom for creating new exhibits, and closingmain widget.
     */
    div
      class:right strip
      widgets
        button
          innerValue:@"<span class=""icon-plus""></span>"
          class:large
          onclick

            /*
             * Invokes file responsible for creating a new exhibit.
             */
            micro.evaluate.file:@INGEN/back-end/exhibits/add-edit-exhibit-view.hl

        button
          class:large
          innerValue:@"<span class=""icon-cross""></span>"
          onclick

            /*
             * Simply deletes view.
             */
            delete-widget:manage-exhibits-wrapper

            /*
             * Clearing toggled state of any toggled Jumbo Buttons.
             */
            micro.evaluate.file:@INGEN/common/toggle-jumbo-button.hl
