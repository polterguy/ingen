/*
 * This file requires a **[folder]** and a **[default-access]** argument, and will list
 * all folders inside of the specified folder, and check for the existence of a _"/jumbo-buttons/"_
 * inside of the children folder of the specified argument.
 *
 * If it finds such a folder, it will invoke **[p5.auth.has-access]**, passing in **[default-access]**
 * as the default access value, to determine if the user has access to the module or not.
 * This allows you to create access objects resembling the following to grant or deny access to
 * specific roles accordingly. The example below will grant access to the _"exhibits"_ backend module
 * to all users belonging to the role of _"super-user"_.
 *
 * ```hyperlambda
 * super-user
 *   ingen.module.allow:/backend/exhibits/
 * ```
 *
 * The namespace for the access objects you create can be either **[ingen.module.allow]**, or
 * **[ingen.module.deny]**, and the path should be either _"/backend/some-backend-module/"_,
 * _"/frontend/some-frontend-module/"_, or _"/common/some-common-module/"_.
 *
 * Only if the user has access it will add any widgets it finds beneath the _"/jumbo-buttons/"_ folder
 * to the main _"desktop launcher"_ of the InGen system.
 *
 * The system will also verify access to modules when a module is loaded, to safeguard against buggy
 * implementations, that for some reasons accidentally evaluates a Hyperlambda file/Active Event it shouldn't
 * have done.
 *
 * If you create your own modules, you should internally invoke **[p5.auth.has-access]** to check if the
 * user has access to the module or not.
 */


/*
 * Sanity checking invocation.
 */
micro.lambda.contract.min:x:/..
  folder:string
  default-access:bool


/*
 * Listing all folders inside of specified [folder] argument.
 */
list-folders:x:/../*/folder?value
for-each:x:/@list-folders/*?name

  /*
   * Figuring out if currently iterated module has a "/jumbo-buttons/"
   * folder or not.
   */
  if
    fetch:x:/0/0?value
      folder-exists:{0}jumbo-buttons/
        :x:/@_dp?value
    not

    /*
     * No Jumbo Buttons for module, continue to next iteration.
     */
    continue

  /*
   * Figuring out if user has access to InGen module or not.
   */
  split:x:/@_dp?value
    =:/
  set:x:/@split(/0|/1|/2)
  join:x:/@split/*?name
    sep:/
  set:x:/@join?value
    src:/{0}/
      :x:/@join?value

  /*
   * TODO: Minor bug in [p5.auth.get-access] doesn't allow us to use x-pressions in main value argument.
   * This is fixed in the source forP5, but not yet released.
   * Remove the [eval-x] invocation below once released version 8.5 of P5.
   */
  eval-x:x:/+
  p5.auth.has-access:x:/../*/default-access?value
    filter:ingen.module
    path:x:/@join?value
  if:x:/@p5.auth.has-access?value
    =:bool:true

    /*
     * User has access to module.
     */
    eval-x:x:/+/*/*
    add:x:/../*/return
      src
        folder:{0}jumbo-buttons/
          :x:/@_dp?value


/*
 * Returning folder(s) to caller.
 */
return