/*
 * Creates our "admin settings" backend widget.
 */


/*
 * Verifying access.
 */
ingen.access._throw-if-no-access:false
  path:/backend/settings/


/*
 * Clearing previously create widgets from dynamic content wrapper widget.
 */
ingen.main-viewport.clear


/*
 * Creating our main widget for allowing admin to modify settings of app.
 */
create-widgets
  micro.widgets.modal:ingen-settings-modal
    widgets
      h2
        innerValue:Settings
      label
        widgets
          input
            type:checkbox
            .data-field:ingen.settings.has-ticket-module
            oninit

              /*
               * Checking if tickets module is turned on.
               */
              if
                ingen.settings.has-ticket-module
                =:bool:true
                set-widget-property:x:/../*/_event?value
                  checked

          span
            innerValue:Ticket module
      label
        widgets
          input
            type:checkbox
            .data-field:ingen.settings.display-tours-jumbo-button
            oninit

              /*
               * Checking if tickets module is turned on.
               */
              if
                ingen-settings.browse-tours-button
                =:bool:true
                set-widget-property:x:/../*/_event?value
                  checked

          span
            innerValue:Browse tours
      div
        class:strip right
        widgets
          button
            innerValue:Save
            onclick

              /*
               * Serialising form.
               */
              micro.form.serialize:ingen-settings-modal

              /*
               * Looping through each setting from above and updating value in database.
               */
              for-each:x:/@micro.form.serialize/*
                delete-data:x:/*/*/{0}
                  :x:/@_dp/#?name
                set:x:/+2/*?name
                  src:x:/@_dp/#?name
                eval-x:x:/+/*/*
                insert-data
                  foo
                    value:x:/@_dp/#?value

              /*
               * Deleting modal window and providing feedback to caller.
               */
              delete-widget:ingen-settings-modal
              desktop.evaluate.on-next-pageload
                lambda
                  micro.windows.info:Settings was updated
                    class:micro-windows-info success

              /*
               * Since some parts of the app might have settings that affects its initial rendering
               * on the page, we do a location reload thing here.
               */
              p5.web.reload-location

          button
            innerValue:Close
            onclick

              /*
               * Simply deleting modal window, and making sure we remove toggled state from Jumbo Button.
               */
              delete-widget:ingen-settings-modal
              micro.evaluate.file:@INGEN/imp/common/toggle-jumbo-button.hl
