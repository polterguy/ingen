/*
 * Allows the user to add or edit a new exhibit.
 */
micro.lambda.contract.optional:x:/..
  id:long


/*
 * Verifying access.
 */
p5.auth.has-access:false
  filter:ingen.module
  path:/backend/exhibits/
if:x:/-?value
  =:bool:false

  /*
   * Oops, access denied!
   */
  throw:Access denied!


/*
 * Checking if caller supplied an [id] argument, at which point we populate the widget
 * with the existing values from that record.
 */
.data
if:x:/../*/id?value

  /*
   * Edit mode.
   */
  eval-x:x:/+/*/*
  add:x:/@.data
    micro.evaluate.file:@INGEN/imp/common/exhibits/model/get-exhibit.hl
      id:x:/../*/id?value


/*
 * Creating modal widget for creating or editing an existing exhibit.
 */
create-widgets
  micro.widgets.modal:themepark-add-exhibit-modal
    widgets
      h2
        innerValue:Exhibit data
      div
        class:strip fill
        widgets
          label
            innerValue:Name
            class:description-9
          input
            type:text
            .data-field:name
            placeholder:Name ...
            value:x:/@.data/*/name?value
            onkeydown:@"if (event.keyCode == 13) {p5.$('add-edit-exhibit-save-button').raise('onclick');return false;} else if (event.keyCode == 27) {p5.$('close-add-edit-exhibit-save-button').raise('onclick');return false;}"
            oninit

              /*
               * Setting initial focus to name widget.
               */
              micro.page.set-focus:x:/../*/_event?value

      div
        class:strip fill
        widgets
          label
            innerValue:Price
            class:description-9
            value:x:/@.data/*/price?value
          input
            type:number
            .data-field:price
            placeholder:Price ...
            value:x:/@.data/*/price?value
            onkeydown:@"if (event.keyCode == 13) {p5.$('add-edit-exhibit-save-button').raise('onclick');return false;} else if (event.keyCode == 27) {p5.$('close-add-edit-exhibit-save-button').raise('onclick');return false;}"
            oninit

              /*
               * Checking if system is configured with the "tickets module", and if not,
               * simply hiding widget entirely
               */
              if
                ingen.settings.has-ticket-module
                =:bool:false
                set-widget-property:x:/../*/_event?value
                  value:0
                p5.web.widgets.find-first-ancestor:x:/../*/_event?value
                  element:div
                set-widget-property:x:/-/*/*?value
                  style:"display:none;"

      div
        class:strip fill
        widgets
          label
            innerValue:Children price
            class:description-9
            value:x:/@.data/*/children_price?value
          input
            type:number
            .data-field:children_price
            placeholder:Children price ...
            value:x:/@.data/*/children_price?value
            onkeydown:@"if (event.keyCode == 13) {p5.$('add-edit-exhibit-save-button').raise('onclick');return false;} else if (event.keyCode == 27) {p5.$('close-add-edit-exhibit-save-button').raise('onclick');return false;}"
            oninit

              /*
               * Checking if system is configured with the "tickets module", and if not,
               * simply hiding widget entirely
               */
              if
                ingen.settings.has-ticket-module
                =:bool:false
                set-widget-property:x:/../*/_event?value
                  value:0
                p5.web.widgets.find-first-ancestor:x:/../*/_event?value
                  element:div
                set-widget-property:x:/-/*/*?value
                  style:"display:none;"

      input
        type:hidden
        value:x:/@.data/*/id?value
        .data-field:id
      label
        innerValue:Description
      micro.widgets.codemirror
        mode:markdown
        .data-field:description
        value:x:/@.data/*/description?value
      div
        class:right strip
        widgets
          button:add-edit-exhibit-save-button
            innerValue:Save
            onclick

              /*
               * Serializing form and invoking file responsible for creating a new record in database.
               */
              micro.form.serialize:themepark-add-exhibit-modal

              /*
               * Checking if this is an edit operation.
               */
              if:x:/@micro.form.serialize/*/id?value
                =:

                /*
                 * Removing [id], since this is not en edit operation.
                 */
                set:x:/@micro.form.serialize/*/id

              /*
               * Invoking file responsible for creating/editing exhibit.
               */
              add:x:/+
                src:x:/@micro.form.serialize/*
              micro.evaluate.file:@INGEN/imp/backend/exhibits/model/add-edit-exhibit.hl

              /*
               * Deleting widget and re-databinding datagrid.
               */
              delete-widget:themepark-add-exhibit-modal
              micro.widgets.mysql.datagrid.databind:manage-exhibits-datagrid

              /*
               * Provding some visual feedback to user.
               */
              micro.windows.info:Successfully updated exhibits
                class:micro-windows-info success

          button:close-add-edit-exhibit-save-button
            innerValue:Close
            onclick

              /*
               * Helper Ajax callback to simply delete modal widget without saving anything.
               */
              delete-widget:themepark-add-exhibit-modal

