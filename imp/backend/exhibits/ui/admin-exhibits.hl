/*
 * Creates our "admin exhibits" backend widget.
 */


/*
 * Verifying access.
 */
p5.auth.has-access:false
  filter:ingen.module
  path:/backend/exhibits/
if:x:/-?value
  =:bool:false

  /*
   * Oops, access denied!
   */
  throw:Access denied!


/*
 * Checking if "manage exhibit" widget already exists, at which point we simply
 * return early.
 */
if
  fetch:x:/0/0?value
    widget-exists:manage-exhibits-wrapper

  /*
   * Widget is already visible.
   */
  return


/*
 * Clearing previously create widgets from dynamic content wrapper widget.
 */
clear-widget:themepark-dynamic-content


/*
 * Setting header and title of page.
 */
themepark.header.set:Administrate exhibits


/*
 * Checking if installation has the "tickets module", and if not, simply deleting all price information
 * from the datagrid.
 */
if
  ingen.settings.has-ticket-module
  =:bool:false
  set:x:/../*/create-widget/*/widgets/*/micro.widgets.mysql.datagrid/=manage-exhibits-datagrid/*/columns/*/price
  set:x:/../*/create-widget/*/widgets/*/micro.widgets.mysql.datagrid/=manage-exhibits-datagrid/*/columns/*/children_price


/*
 * Creating our "manage exhibit" datagrid in a "bg" wrapper div.
 */
create-widget:manage-exhibits-wrapper
  parent:themepark-dynamic-content
  class:shaded rounded air air-inner bg
  widgets

    /*
     * Our actual datagrid, dynamically databound towards "exhibits" table.
     */
    micro.widgets.mysql.datagrid:manage-exhibits-datagrid
      class:hover themepark-datagrid
      database:ingen
      table:exhibits
      .row
        onclick

          /*
           * Invoking file responsible for editing a specific item from database.
           */
          eval-x:x:/+/*
          micro.evaluate.file:@INGEN/imp/backend/exhibits/ui/add-edit-exhibit.hl
            id:x:/../*/.row/*/id?value

      columns
        id
          visible:bool:false
        name
          .header
            innerValue:Name
        price
          visible:bool:false
        price
          style:"width:5px;"
          .lambda
            eval-x:x:/+/*/*
            return
              span
                innerValue:€{0}
                  :x:/../*/row/*/price?value
          .header
            innerValue:Adults
        children_price
          visible:bool:false
        children_price
          style:"width:5px;"
          .lambda
            eval-x:x:/+/*/*
            return
              span
                innerValue:€{0}
                  :x:/../*/row/*/children_price?value
          .header
            innerValue:Children
        Delete
          style:"width:5px;"
          .lambda
            eval-x:x:/+/*/*/onclick/*/.id
            return
              button
                innerValue:@"<span class=""icon-bin""></span>"
                onclick

                  /*
                   * Forward evaluated above.
                   */
                  .id:x:/../*/row/*/id?value

                  /*
                   * Invoking file responsible for deleting exhibit.
                   */
                  eval-x:x:/+/*
                  micro.evaluate.file:@INGEN/imp/backend/exhibits/model/delete-exhibit.hl
                    id:x:/@.id?value

                  /*
                   * Re-databinding datagrid.
                   */
                  micro.widgets.mysql.datagrid.databind:manage-exhibits-datagrid

                  /*
                   * Provding some visual feedback to user.
                   */
                  micro.windows.info:Successfully updated exhibits
                    class:micro-windows-info success

    /*
     * Toolbar at the bottom for creating new exhibits, and closingmain widget.
     */
    div
      class:right strip
      widgets
        button
          innerValue:@"<span class=""icon-plus""></span>"
          class:large
          onclick

            /*
             * Invokes file responsible for creating a new exhibit.
             */
            micro.evaluate.file:@INGEN/imp/backend/exhibits/ui/add-edit-exhibit.hl
