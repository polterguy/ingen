/*
 * Creates our "admin tours" back end widget.
 */


/*
 * Verifying access.
 */
ingen.access._throw-if-no-access:false
  path:/backend/tours/


/*
 * Checking if "manage tours" widget already exists, at which point we simply
 * return early.
 */
if
  fetch:x:/0/0?value
    widget-exists:manage-tours-wrapper

  /*
   * Widget is already visible.
   */
  return


/*
 * Clearing previously create widgets from dynamic content wrapper widget.
 */
ingen.main-viewport.clear


/*
 * Setting header and title of page.
 */
ingen.header.set:Administrate tours


/*
 * Creating our "manage tours" datagrid in a "bg" wrapper div.
 */
create-widget:manage-tours-wrapper
  parent:themepark-dynamic-content
  class:shaded rounded air air-inner bg
  widgets

    /*
     * This is the "tours filter" wrapper widget.
     *
     * Since we're using this in both the front end and the back end, we've created
     * an extension widget, to keep our code "DRY".
     *
     * If we wanted to further improve upon this logic, we could probably even share the
     * same filter/paging toolbar between also for instance the administration of tickets.
     * However, this has been left as an exercise if you want to do this.
     */
    ingen.widgets.tours._datagrid-filter

    /*
     * Our actual datagrid, dynamically databound towards "exhibits" table.
     *
     * Notice, our [onclick] is what occurs when a row in our datagrid is clicked.
     * The [columns] declaration as simply appended to the existing common columns
     * for our datagrid - Which we take advantage of to make it possible to delete items.
     */
    ingen.widgets.tours._datagrid:tours-datagrid
      onclick

        /*
         * Invoking file responsible for editing a specific item from database.
         */
        eval-x:x:/+/*
        micro.evaluate.file:@INGEN/imp/backend/tours/ui/add-edit-tours.hl
          id:x:/../*/.row/*/id?value

      columns

        /*
         * Delete column containing a simple "delete record" button.
         */
        Delete
          style:"width:5px;"
          .lambda
            eval-x:x:/+/*/*/onclick/**/.id
            return
              button
                innerValue:@"<span class=""icon-bin""></span>"
                onclick

                  /*
                   * Asking user to confirm deletion.
                   */
                  micro.windows.confirm
                    header:Confirm deletion
                    body:Are you sure you want to delete this tour? This action is permanent, and cannot be undone.
                    onok

                      /*
                       * Forward evaluated above.
                       */
                      .id:x:/../*/row/*/id?value

                      /*
                       * Invoking file responsible for deleting exhibit.
                       */
                      eval-x:x:/+/*
                      micro.evaluate.file:@INGEN/imp/backend/tours/model/delete-tour.hl
                        id:x:/@.id?value

                      /*
                       * Re-databinding datagrid.
                       */
                      ingen.widgets.tours._datagrid.databind

                      /*
                       * Provding some visual feedback to user.
                       */
                      micro.windows.info:Successfully deleted tour
                        class:micro-windows-info success

    /*
     * Toolbar at the bottom for creating new tours, and closing main widget.
     */
    div
      class:right strip
      widgets
        button
          innerValue:@"<span class=""icon-plus""></span>"
          class:large
          onclick

            /*
             * Invokes file responsible for creating a new tours.
             */
            micro.evaluate.file:@INGEN/imp/backend/tours/ui/add-edit-tours.hl
