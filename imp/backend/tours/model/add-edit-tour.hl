/*
 * File responsible for creating a new or editing an existing database
 * entry of type "tours".
 */


/*
 * Sanity checking arguments.
 */
micro.lambda.contract.min:x:/..
  exhibit_id:long
  name:string
  content:string
micro.lambda.contract.optional:x:/..
  id:long


/*
 * Connecting to ingen database and inserting new "exhibit" item.
 */
p5.mysql.connect:[ingen]

  /*
   * Checking if this is an update or an insert operation.
   */
  if:x:/../*/id?value
    and:x:/../*/id?value
      !=:

    /*
     * Update operation.
     */
    p5.mysql.update:update tours set name = @name, content = @content, exhibit_id = @exhibit_id where id = @id
      @name:x:/../*/name?value
      @content:x:/../*/content?value
      @exhibit_id:x:/../*/exhibit_id?value.long
      @id:x:/../*/id?value.long

  else

    /*
     * Insert operation.
     */
    p5.mysql.insert:insert into tours (name, content, exhibit_id) value (@name, @content, @exhibit_id)
      @name:x:/../*/name?value
      @content:x:/../*/content?value
      @exhibit_id:x:/../*/exhibit_id?value
