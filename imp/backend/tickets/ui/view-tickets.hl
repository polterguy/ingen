
/*
 * Displays ticket information in a modal window and allows operator
 * to mark tickets as used.
 */


/*
 * Sanity checking invocation.
 */
micro.lambda.contract.min:x:/..
  id:long


/*
 * Retrieving ticket information.
 */
eval-x:x:/+/*
micro.evaluate.file:@INGEN/imp/backend/tickets/model/get-ticket.hl
  id:x:/../*/id?value


/*
 * Creates a modal window displaying ticket information.
 */
eval-x:x:/+/**/.id
create-widgets
  micro.widgets.modal:view-tickets
    class:micro-widgets-modal larger-text
    widgets
      h2
        innerValue:x:/@micro.evaluate.file/*/exhibit?value
      p
        innerValue:"Name: {0}"
          :x:/@micro.evaluate.file/*/name?value
      p
        innerValue:"Email: {0}"
          :x:/@micro.evaluate.file/*/email?value
      p
        innerValue:"Adults: {0}"
          :x:/@micro.evaluate.file/*/adults?value
      p
        innerValue:"Children: {0}"
          :x:/@micro.evaluate.file/*/children?value
      div
        class:right strip
        widgets
          button
            innerValue:Mark as used
            oninit

              /*
               * Setting initial focus to "mark as used" button.
               */
              micro.page.set-focus:x:/../*/_event?value

            onclick

              /*
               * Forward evaluated above.
               */
              .id:x:/../*/id?value

              /*
               * Making sure we set all tickets in collection to "used".
               */
              eval-x:x:/+/*
              micro.evaluate.file:@INGEN/imp/backend/tickets/model/set-all-tickets-used.hl
                id:x:/../*/.id?value

              /*
               * If we came this far, all tickets were used, and we can safely re-databind
               * the tickets datagrid.
               */
              themepark.tickets.re-databind

              /*
               * Deleting modal widget.
               */
              delete-widget:view-tickets

          button
            innerValue:Close
            onclick

              /*
               * Simply deleting modal widget.
               */
              delete-widget:view-tickets
