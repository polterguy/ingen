/*
 * Creates the **[ingen.widgets.exhibits._datagrid]** extension widget, to make sure we keep our
 * code "DRY", since the differences between the frontend version and the backend version are
 * minimal.
 *
 * Expects **[onclick]** lambda callback, which is invoked when a row in datagrid is clicked,
 * in addition to (optionally) a bunch of additional **[columns]**, which will be appended
 * into the widget, in addition to its default columns collection.
 */
create-event:ingen.widgets.exhibits._datagrid

  /*
   * Sanity checking invocation.
   */
  micro.lambda.contract.min:x:/..
    onclick

  /*
   * Checking if installation has the "tickets module", and if not, simply deleting all price information
   * from the datagrid.
   */
  if
    ingen.settings.has-ticket-module
    =:bool:false

    /*
     * Tickets module is not enabled, hence we remove all tickets columns from datagrid.
     */
    set:x:/../*/return/**/micro.widgets.mysql.datagrid/*/columns/*/price
    set:x:/../*/return/**/micro.widgets.mysql.datagrid/*/columns/*/children_price

  /*
   * Decorating [onclick] of row.
   */
  add:x:/../*/return/*/*/.row
    src:x:/../*/onclick

  /*
   * Adding additional columns.
   */
  add:x:/../*/return/*/*/columns
    src:x:/../*/columns/*

  /*
   * Returning widget to caller.
   */
  return
    micro.widgets.mysql.datagrid
      class:hover themepark-datagrid
      database:ingen
      databind:bool:false
      page-size:10
      events

        /*
         * Databinds exhibits.
         */
        ingen.widgets.exhibits._datagrid.databind

          /*
           * Checking if we have a filter.
           */
          ingen.widgets.exhibits._datagrid-filter.get-filter
          if:x:/@ingen.widgets.exhibits._datagrid-filter.get-filter?value

            /*
             * Adding filter to databind invocation.
             */
            add:x:/../*/micro.widgets.mysql.datagrid.databind
              src
                filter:name like @name
            eval-x:x:/+/*/*
            add:x:/../*/micro.widgets.mysql.datagrid.databind/*/filter
              src
                @name:%{0}%
                  :x:/@ingen.widgets.exhibits._datagrid-filter.get-filter?value

          /*
           * Retrieving page.
           */
          ingen.widgets.exhibits._datagrid-filter.get-page
          eval-x:x:/+/*/*
          add:x:/../*/micro.widgets.mysql.datagrid.databind
            src
              page:x:/@ingen.widgets.exhibits._datagrid-filter.get-page?value.long

          /*
           * Databinding exhibits grid, and returning results to caller.
           */
          micro.widgets.mysql.datagrid.databind:exhibits-datagrid

          /*
           * Checking if we "previous paging buttons" exists, and if it does, checking if
           * we should enable disable it.
           */
          if
            fetch:x:/0/0?value
              widget-exists:exhibits-page-previous

            /*
             * Previous paging button exists, now checking if we should enable or disable it.
             */
            if:x:/@ingen.widgets.exhibits._datagrid-filter.get-page?value.long
              >:long:0

              /*
               * Enabling previous button.
               */
              delete-widget-property:exhibits-page-previous
                disabled

            else

              /*
               * Disabling previous button.
               */
              set-widget-property:exhibits-page-previous
                disabled

          /*
           * Checking if we "next paging buttons" exists, and if it does, checking if
           * we should enable disable it.
           */
          if
            fetch:x:/0/0?value
              widget-exists:exhibits-page-next

            /*
             * Previous paging button exists, now checking if we should enable or disable it.
             */
            if:x:/@micro.widgets.mysql.datagrid.databind/*/more-pages?value
              =:bool:true

              /*
               * Enabling previous button.
               */
              delete-widget-property:exhibits-page-next
                disabled

            else

              /*
               * Disabling previous button.
               */
              set-widget-property:exhibits-page-next
                disabled

          /*
           * Returning results from above invocation to caller.
           */
          return:x:/@micro.widgets.mysql.datagrid.databind/*

      table:exhibits
      .row
      columns
        id
          visible:bool:false
        Name
        price
          visible:bool:false
        price
          style:"width:5px;"
          .lambda
            eval-x:x:/+/*/*
            return
              span
                innerValue:€{0}
                  :x:/../*/row/*/price?value
          .header
            innerValue:Adults
        children_price
          visible:bool:false
        children_price
          style:"width:5px;"
          .lambda
            eval-x:x:/+/*/*
            return
              span
                innerValue:€{0}
                  :x:/../*/row/*/children_price?value
          .header
            innerValue:Children
