/*
 * Creates our "view tours" front end widget.
 */


/*
 * Verifying access.
 */
p5.auth.has-access:true
  filter:ingen.module
  path:/frontend/tours/
if:x:/-?value
  =:bool:false

  /*
   * Oops, access denied!
   */
  throw:Access denied!


/*
 * Checking if "view tour" widget already exists, at which point we simply
 * return early.
 */
if
  fetch:x:/0/0?value
    widget-exists:view-tours-wrapper

  /*
   * Widget is already visible.
   */
  return


/*
 * Clearing previously create widgets from dynamic content wrapper widget.
 */
clear-widget:themepark-dynamic-content


/*
 * Setting header and title of page.
 */
themepark.header.set:Tours


/*
 * Creating our "view tour" datagrid in a "bg" wrapper div.
 */
create-widget:view-tours-wrapper
  parent:themepark-dynamic-content
  class:shaded rounded air air-inner bg
  widgets

    /*
     * Our actual datagrid, dynamically databound towards "tours" table.
     */
    micro.widgets.mysql.datagrid:view-tours-datagrid
      class:hover themepark-datagrid
      database:ingen
      table:tours
      .row
        onclick

          /*
           * Evaluating file responsible for displaying information for tour,
           * and allowing user to purchase ticket(s) to tour.
           */
          eval-x:x:/+/*
          micro.evaluate.file:@INGEN/imp/frontend/tours/ui/view-single-tour.hl
            id:x:/../*/.row/*/id?value

      columns
        id
          visible:bool:false
        Name
