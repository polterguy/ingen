/*
 * Creates our "view exhibits" front end widget.
 */


/*
 * Verifying access.
 */
p5.auth.has-access:true
  filter:ingen.module
  path:/frontend/exhibits/
if:x:/-?value
  =:bool:false

  /*
   * Oops, access denied!
   */
  throw:Access denied!


/*
 * Checking if "view exhibit" widget already exists, at which point we simply
 * return early.
 */
if
  fetch:x:/0/0?value
    widget-exists:view-exhibits-wrapper

  /*
   * Widget is already visible.
   */
  return


/*
 * Clearing previously create widgets from dynamic content wrapper widget.
 */
clear-widget:themepark-dynamic-content


/*
 * Setting header and title of page.
 */
themepark.header.set:Exhibits


/*
 * Verifying access to ticket module, and if access is not granted, we
 * make sure we remove the pricing columns of the datagrid.
 *
 * This make it easy to create a system where there are no tickets requirements, to only
 * let the guests view the exhibits, and take the tours, etc.
 */
p5.auth.has-access:true
  filter:ingen.module
  path:/frontend/tickets/
if:x:/-?value
  =:bool:false
  or
    ingen.settings.has-ticket-module
    =:bool:false

  /*
   * Removing every trace related to pricing of exhibits from our datagrid.
   */
  set:x:/../*/create-widget/*/widgets/*/micro.widgets.mysql.datagrid/=view-exhibits-datagrid/*/columns/*/price
  set:x:/../*/create-widget/*/widgets/*/micro.widgets.mysql.datagrid/=view-exhibits-datagrid/*/columns/*/children_price


/*
 * Creating our "view exhibit" datagrid in a "bg" wrapper div.
 */
create-widget:view-exhibits-wrapper
  parent:themepark-dynamic-content
  class:shaded rounded air air-inner bg
  widgets

    /*
     * This is the "exhibit filter" wrapper widget.
     */
    div:exhibits-filter-wrapper
      .page:0
      class:strip fill themepark-large-strip
      widgets
        input:exhibits-filter
          type:text
          placeholder:Search ...
          onkeydown:@"if (event.keyCode == 13) {p5.$('exhibits-filter-button').raise('onclick');return false;}"
        button:exhibits-filter-button
          class:large
          innerValue:@"<span style=""font-size:1.3rem;"" class=""icon-search""></span>"
          onclick

            /*
             * Setting [.page] to 0, and disabling the previous button.
             */
            set-widget-property:exhibits-filter-wrapper
              .page:0
            set-widget-property:exhibits-page-previous
              disabled

            /*
             * Databinding exhibits.
             */
            themepark.exhibits.databind

            /*
             * Checking if we should enabled the next button or not.
             */
            if:x:/@themepark.exhibits.databind/*/more-pages?value
              =:bool:true

              /*
               * Enabling next button.
               */
              delete-widget-property:exhibits-page-next
                disabled

            else

              /*
               * Disabling next button.
               */
              set-widget-property:exhibits-page-next
                disabled

            /*
             * Setting focus to filter textbox again, to make it easily removed.
             */
            micro.page.set-focus:exhibits-filter

        button:exhibits-page-previous
          class:large
          innerValue:@"<span style=""font-size:1.3rem;"" class=""icon-arrow-left""></span>"
          disabled
          onclick

            /*
             * Retrieving current page, subtracting 10 from it, and re-databinding grid.
             */
            get-widget-property:exhibits-filter-wrapper
              .page
            if:x:/@get-widget-property/*/*?value.long
              =:long:0

              /*
               * Beginning of dataset, returning early.
               */
              return

            /*
             * Subtracting 1 from current [.page] value, and updating attribute.
             */
            -:x:/@get-widget-property/*/*?value.long
              _:1
            set-widget-property:exhibits-filter-wrapper
              .page:x:/@-?value

            /*
             * Checking if we should disabled the previous button.
             */
            if:x:/@-?value.long
              =:long:0

              /*
               * At the beginning of dataset, hence disabling previous button.
               */
              set-widget-property:exhibits-page-previous
                disabled

            /*
             * Re-databinding grid and making sure we enable the "next page" button.
             */
            themepark.exhibits.databind
            delete-widget-property:exhibits-page-next
              disabled

        button:exhibits-page-next
          class:large
          innerValue:@"<span style=""font-size:1.3rem;"" class=""icon-arrow-right""></span>"
          onclick

            /*
             * Retrieving current page, adding 10 to it, and re-databinding grid.
             */
            get-widget-property:exhibits-filter-wrapper
              .page
            +:x:/@get-widget-property/*/*?value.long
              _:1
            set-widget-property:exhibits-filter-wrapper
              .page:x:/@+?value
            themepark.exhibits.databind

            /*
             * Checking if we should disabled next button.
             */
            if:x:/@themepark.exhibits.databind/*/more-pages?value
              =:bool:false

              /*
               * Disabling next button since there are no more pages.
               */
              set-widget-property:exhibits-page-next
                disabled

            /*
             * Enabling previous button.
             */
            delete-widget-property:exhibits-page-previous
              disabled

    /*
     * Our actual datagrid, dynamically databound towards "exhibits" table.
     */
    micro.widgets.mysql.datagrid:view-exhibits-datagrid
      class:hover themepark-datagrid
      database:ingen
      databind:bool:false
      page-size:10
      oninit

        /*
         * Initially databinding exhibits.
         */
        themepark.exhibits.databind

        /*
         * Checking if there are more than one page in database, and if not,
         * simply deleting both paging buttons.
         */
        if:x:/-/*/more-pages?value
          =:bool:false

          /*
           * No reasons to display paging buttons since there's only one page of items in database.
           */
          delete-widget:exhibits-page-previous
          delete-widget:exhibits-page-next

      events

        /*
         * Databinds exhibits.
         */
        themepark.exhibits.databind

          /*
           * Checking if we have a filter.
           */
          get-widget-property:exhibits-filter
            value
          if:x:/@get-widget-property/*/*?value
            and:x:/@get-widget-property/*/*?value
              !=:

            /*
             * Adding filter to databind invocation.
             */
            add:x:/../*/micro.widgets.mysql.datagrid.databind
              src
                filter:name like @name
            eval-x:x:/+/*/*
            add:x:/../*/micro.widgets.mysql.datagrid.databind/*/filter
              src
                @name:%{0}%
                  :x:/@get-widget-property/*/*?value

          /*
           * Retrieving page.
           */
          get-widget-property:exhibits-filter-wrapper
            .page
          eval-x:x:/+/*/*
          add:x:/../*/micro.widgets.mysql.datagrid.databind
            src
              page:x:/@get-widget-property/*/*?value.long

          /*
           * Databinding exhibits grid, and returning results to caller.
           */
          micro.widgets.mysql.datagrid.databind:view-exhibits-datagrid
          return:x:/-/*

      table:exhibits
      .row
        onclick

          /*
           * Evaluating file responsible for displaying information for exhibit,
           * and allowing user to purchase ticket(s) to exhibit.
           */
          eval-x:x:/+/*
          micro.evaluate.file:@INGEN/imp/frontend/exhibits/ui/view-single-exhibit.hl
            id:x:/../*/.row/*/id?value

      columns
        id
          visible:bool:false
        Name
        price
          visible:bool:false
        price
          style:"width:5px;"
          .lambda
            eval-x:x:/+/*/*
            return
              span
                innerValue:€{0}
                  :x:/../*/row/*/price?value
          .header
            innerValue:Adults
        children_price
          visible:bool:false
        children_price
          style:"width:5px;"
          .lambda
            eval-x:x:/+/*/*
            return
              span
                innerValue:€{0}
                  :x:/../*/row/*/children_price?value
          .header
            innerValue:Children

    /*
     * This is where we display exhibits as the user selects an exhibit to view it.
     */
    container:exhibits-display-single-exhibit
