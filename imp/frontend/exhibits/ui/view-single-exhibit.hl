/*
 * File responsible for viewing a single exhibit.
 */


/*
 * Sanity checking invocation.
 */
micro.lambda.contract.min:x:/..
  id:long


/*
 * Verifying access.
 */
ingen.access._throw-if-no-access:true
  path:/frontend/exhibits/


/*
 * Making sure we remove eny previously created exhibits.
 */
clear-widget:exhibits-display-single-exhibit


/*
 * Retrieving exhibit from data layer.
 */
eval-x:x:/+/*
micro.evaluate.file:@INGEN/imp/common/exhibits/model/get-exhibit.hl
  id:x:/../*/id?value


/*
 * Transforming description markdownto HTML.
 */
micro.markdown.markdown2html:x:/@micro.evaluate.file/*/description?value


/*
 * Verifying access to ticket module, and if access is not granted, we
 * delete the "purchase ticket" wrapper.
 *
 * This make it easy to create a system where there are no tickets requirements, to only
 * let the guests view the exhibits, and take the tours, etc.
 */
p5.auth.has-access:true
  filter:ingen.module
  path:/frontend/tickets/
if:x:/-?value
  =:bool:false
  or
    ingen.settings.has-ticket-module
    =:bool:false

  /*
   * Oops, access denied to ticket purchasing module.
   *
   * Hence, simply removing the "purchase ticket to exhibit" button.
   */
  set:x:/../*/create-widgets/**/div/=purchase-ticket-wrapper

  /*
   * Also removing pricing information in header of modal window.
   */
  set:x:/../*/create-widgets/**/h1/=exhibit-header/*/widgets/*/small


/*
 * Displaying exhibit information in a modal widget, and allowing user to purchase ticket(s) for exhibit.
 */
eval-x:x:/+/**/.id
create-widgets
  div:view-exhibit-wrapper
    parent:exhibits-display-single-exhibit
    widgets
      h1:exhibit-header
        widgets
          span
            innerValue:x:/@micro.evaluate.file/*/name?value
          small
            innerValue:@" - Aults €{0}, Children €{1}"
              :x:/@micro.evaluate.file/*/price?value
              :x:/@micro.evaluate.file/*/children_price?value
      div
        class:themepark-view-single-exhibit
        innerValue:x:/@micro.markdown.markdown2html?value

      /*
       * Purchase ticket(s) jumbo button.
       */
      div:purchase-ticket-wrapper
        class:center
        widgets
          button
            class:jumbo-button
            innerValue:@"<span class=""icon-cart""></span> Tickets"
            onclick

              /*
               * Forward evaluated above.
               */
              .id:x:/../*/id?value

              /*
               * Evaluating file responsible for allowing the user to order ticket(s) for the exhibit.
               */
              eval-x:x:/+/*
              micro.evaluate.file:@INGEN/imp/frontend/tickets/ui/purchase-tickets.hl
                id:x:/@.id?value

      /*
       * Close button wrapper.
       */
      div
        class:right
        widgets
          button
            innerValue:Close
            class:large
            onclick

              /*
               * Deleting exhibit widget.
               */
              delete-widget:view-exhibit-wrapper


/*
 * Making sure we scroll exhibit into view.
 */
micro.page.scroll-into-view:exhibit-header